/*
 * @TODO:
 * - The back to Geoscape button actually takes you back to base. When putting an extra ui_pop in, it
 *   takes you to campaign options (load, save, etc) instead of geoscape.
 * - After clicking on a UFOpaedia link for an item, the base names for external research disappear.
 * - Scientists hired are not correct values, just place-holder text
 * --- Need new code in the callbacks to get hired/unhired scientists and to hire/fire them.
 * - Make research scrollable and find a way to test it.
 * - Clicking on another base name should take the player to that base (see research_changebase).
 * -- Maybe we need an icon that suggests clicking will switch bases.
 */

window research
{
	{ size	"1024 768" }

	// Base name and container panel
	panel base_container
	{
		{
			pos				"5 5"
			size			"723 758"
			border			"2"
			bordercolor		"0.56 0.81 0.76 1"
		}

		// @TODO: make next/prev base btns work
		button prev
		{
			icon			"icons/windowprev"
			tooltip			"_Previous base"
			pos				"90 6"
			size			"18	18"
			onClick			{ cmd "ui_pop; mn_base_prev;" }
		}
		button next
		{
			icon			"icons/windownext"
			tooltip			"_Next base"
			pos				"115 6"
			size			"18	18"
			onClick			{ cmd "ui_pop; mn_base_next;" }
		}
		// Base name
		textentry name
		{
			string		*cvar:mn_base_title
			pos			"135 0"
			size		"465 32"
			font		"f_small_bold"
			color			"0.56 0.81 0.76 1"
			contentalign	ALIGN_CL
			onChange 	{ cmd "base_changename" } 	// stores the value of the cvar in the base structure
		}
		// Base selector
		panel base_selector
		{
			{
				pos		"440 7"
				size	"160 16"
				layout			LAYOUT_COLUMN
				layoutColumns	8
				layoutMargin	4
			}
			BaseLayoutMini base0 { baseid 0 }
			BaseLayoutMini base1 { baseid 1 }
			BaseLayoutMini base2 { baseid 2 }
			BaseLayoutMini base3 { baseid 3 }
			BaseLayoutMini base4 { baseid 4 }
			BaseLayoutMini base5 { baseid 5 }
			BaseLayoutMini base6 { baseid 6 }
			BaseLayoutMini base7 { baseid 7 }
		}
		string credits
		{
			string		*cvar:mn_credits
			pos			"600 0"
			size		"120 32"
			font		"f_small_bold"
			color			"0.56 0.81 0.76 1"
			contentalign	ALIGN_CR
		}

		// Research panel
		panel research
		{
			{
				pos				"4 30"
				size			"715 590"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
			}
			// Title bar
			panel header
			{
				{
					pos			"1 1"
					size		"713 32"
					bgcolor		"0.527 0.6 0.21 0.2"
				}
				string title
				{
					string			"_Research"
					pos				"90 1"
					size			"593 32"
					contentalign	ALIGN_CL
					color			"0.56 0.81 0.76 1"
				}
				button back
				{
					icon			"icons/windownext"
					tooltip			"_Back to base"
					pos				"683 7"
					size			"18	18"
					onClick			{ cmd "ui_pop; base_init; reset_base;" }
				}
			}

			// Unassigned scientists
			button idle_scientists
			{
				icon	"icons/head_scientist"
				pos		"522 40"
				size	"48 48"
				ghost	true
			}
			string idle_scientists_count
			{
				string	*cvar:mn_scientists_available
				pos		"560 49"
				size	"100 25"
				font	"f_small_bold"
				color	"0.56 0.81 0.76 1"
				contentalign	ALIGN_CL
			}
			string idle_scientists_label
			{
				string	"_idle scientists"
				pos		"560 62"
				size	"100 23"
				font	"f_verysmall"
				color	"0.56 0.81 0.76 1"
				contentalign	ALIGN_CL
			}

			// List of research techs
			panel techs
			{
				{
					pos				"1 80"
					size			"713 420"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
				}
			}

			// Base scientists/lab stats
			panel base_stats
			{
				{
					pos		"1 505"
					size	"713 84"
				}
				panel scientists
				{
					{
						pos		"5 0"
						size	"350 84"
					}
					button scientist
					{
						icon	"icons/head_scientist"
						pos		"0 0"
						size	"48 48"
					}
					string scientists_count
					{
						string	*cvar:mn_scientists_available_string
						pos		"40 9"
						size	"300 25"
						font	"f_small_bold"
						color	"0.56 0.81 0.76 1"
						contentalign	ALIGN_CL
					}
					texture data_bg
					{
						pos			"43 29"
						size		"282 8"
						src			"ui/bar_bg"
						ghost		true
					}
					bar data_bar
					{
						pos			"43 29"
						size		"282 8"
						color		"0.56 0.81 0.76 1"
						current		*cvar:mn_scientists_hired
						max			*cvar:mn_total_scientists
						ghost		true
					}
					MainMenu3Btn hire
					{
						string			"_HIRE"
						pos				"250 42"
						size			"75 26"
						font			"f_small_bold"
						color			"0 0 0 0.9"
						selectcolor		"0 0 0 1"
						onClick			{ cmd "hire_last_scientist;" }
					}
					MainMenuExitBtn fire
					{
						string			"_FIRE"
						pos				"165 42"
						size			"75 26"
						font			"f_small_bold"
						color			"0 0 0 0.9"
						selectcolor		"0 0 0 1"
						onClick			{ cmd "fire_last_scientist;" }
					}
				}
				panel lab_space
				{
					{
						pos		"365 0"
						size	"350 84"
					}
					button labs
					{
						icon	"icons/research"
						pos		"0 0"
						size	"48 48"
					}
					string labs_space_current
					{
						string	*cvar:mn_lab_space_string
						pos		"40 9"
						size	"200 25"
						font	"f_small_bold"
						color	"0.56 0.81 0.76 1"
						contentalign	ALIGN_CL
					}
					texture data_bg
					{
						pos			"43 29"
						size		"282 8"
						src			"ui/bar_bg"
						ghost		true
					}
					bar data_bar
					{
						pos			"43 29"
						size		"282 8"
						color		"0.56 0.81 0.76 1"
						current		*cvar:mn_current_labspace
						max			*cvar:mn_max_labspace
						ghost		true
					}
					MainMenu3Btn build_lab
					{
						string			"_BUILD LAB"
						pos				"175 42"
						size			"150 26"
						font			"f_small_bold"
						color			"0 0 0 0.9"
						selectcolor		"0 0 0 1"
						onClick			{ cmd "ui_pop; build_facility true;" }
					}
				}
			}

			/*
			 * Add research techs to the list for this base
			 * param1 = nnumber of item in research tech list (see cp_research_callbacks.c)
			 */
			confunc research_research
			{
				call *node:parent.techs@createChild("tech<1>", "ResearchItem")
				*node:parent.techs.tech<1>@tooltip = *cvar:mn_rsstatus<1>
				*node:parent.techs.tech<1>.title@string = *cvar:mn_researchitem<1>
				*node:parent.techs.tech<1>.status@current = *cvar:mn_rsstatus<1>
				*node:parent.techs.tech<1>.scientists@string = *cvar:mn_researchassigned<1>
				*node:parent.techs.tech<1>.itemnum@integer = <1>
			}
		}

		// Research in other bases panel
		panel research_ext
		{
			{
				pos				"4 629"
				size			"715 125"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
			}
			// Title bar
			panel header
			{
				{
					pos			"1 1"
					size		"713 32"
					bgcolor		"0.527 0.6 0.21 0.2"
				}
				string title
				{
					string			"_Other Bases"
					pos				"30 1"
					size			"593 32"
					contentalign	ALIGN_CL
					color			"0.56 0.81 0.76 1"
				}
			}

			// Research on other bases
			panel techs_ext
			{
				{
					pos				"1 34"
					size			"713 90"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
				}
			}
			/*
			 * Get the base name before listing research options
			 * param1 = number of item in research tech list (see cp_research_callbacks.c)
			 */
			confunc research_basetitle
			{
				*cvar:rs_cur_base_itemid = <1>
			}
			/*
			 * Add items being researched in other bases
			 * param1 = number of item in research tech list (see cp_research_callbacks.c)
			 */
			confunc research_outterresearch
			{
				call *node:parent.techs_ext@createChild("tech<1>", "ExtResearchItem")
				*node:parent.techs_ext.tech<1>@tooltip = *cvar:mn_rsstatus<1>
				*node:parent.techs_ext.tech<1>.title@string = *cvar:mn_researchitem<1>
				*node:parent.techs_ext.tech<1>.status@current = *cvar:mn_rsstatus<1>
				*node:parent.techs_ext.tech<1>.base@string = *cvar:mn_researchitem<cvar:rs_cur_base_itemid>
				*node:parent.techs_ext.tech<1>.itemnum@integer = <1>
			}
		}

		/**
		 * @brief Switch to another reserch base
		 * @param[in] <1> New base id
		 * @param[in] <2> Selected item row
		 */
		confunc research_changebase {
			cmd "ui_pop;ui_pop;mn_base_select <1>;ui_push research;"
			cmd "research_select <2>;"
		}

		// Intended to make an empty line.
		confunc research_hide { }

		// Intended to set number of unassigned scientists in bases.
		confunc research_baseinfo { }

		// Other unused confuncs for items that can't be researched.
		confunc research_missingitem { }
		confunc research_missingitemtitle { }
		confunc research_unresearchableitem { }
		confunc research_unresearchableitemtitle { }

		// Called on selected item (highlight selected)
		confunc research_selected { }

		// Set in cp_research_callbacks.c to show research status at the moment
		// Under research, paused, not started yet.
		confunc research_running { }
		confunc research_paused { }
		confunc research_normal { }

		//Intended to update values for an item, called with research_init
		confunc research_updateitem { }
	}

	// Research item display panel
	panel res_item
	{
		{
			pos		"739 0"
			size	"277 768"
		}
		image img
		{
			src			*cvar:mn_research_image
			pos				"0 80"
			size			"277 632"
			preventratio	true
			contentalign	ALIGN_UC
		}
		model mdl
		{
			src				*cvar:mn_research_model
			angles			"-30 135 45"
			pos				"0 80"
			size			"277 277"
			autoscale		true
		}
		string label
		{
			string			*cvar:mn_research_selname
			pos				"0 80"
			size			"277 30"
			color			"0.56 0.81 0.76 1"
			bgcolor			"0 0 0 0.6"
			contentalign	ALIGN_CC
		}
	}

	// Back to geoscape
	Geoscape geoscape_button { }

	// Fuzzy screen effect (see assets.ufo)
	screen overlay { }

	/*
	 * Concatenates the string for lab space
	 * Called from onWindowOpened, but must be in a confunc so that it is
	 * executed after the cvars have been set
	 */
	confunc get_lab_space_string
	{
		*cvar:mn_lab_space_string = "<cvar:mn_current_labspace> / <cvar:mn_max_labspace> laboratory space"
	}

	/*
	 * Gets the string for hired/total scientists
	 * param1 = hired
	 * param2 = available
	 */
	confunc available_scientists
	{
		*cvar:mn_total_scientists = ( *cvar:mn_scientists_hired + *cvar:mn_scientists_unhired )
		*cvar:mn_scientists_available_string = "<cvar:mn_scientists_hired> / <cvar:mn_total_scientists> scientists hired"
	}

	// Fire last scientist
	confunc fire_last_scientist
	{
		cmd "employee_fire_last 1 on_fire_last_scientist;"
	}
	
	// Hire first scientist
	confunc hire_last_scientist
	{
		*cvar:mn_temp = ( *cvar:mn_scientists_hired + 1 )
		cmd "employee_hire_first 1 on_hire_last_scientist;"
	}
	
	confunc on_fire_last_scientist
	{
		*cvar:mn_scientists_hired = ( *cvar:mn_scientists_hired - 1 )
		*cvar:mn_scientists_unhired = ( *cvar:mn_scientists_unhired + 1 )
		*cvar:mn_total_scientists = ( *cvar:mn_scientists_hired + *cvar:mn_scientists_unhired )
		*cvar:mn_scientists_available_string = "<cvar:mn_scientists_hired> / <cvar:mn_total_scientists> scientists hired"
	}

	confunc on_hire_last_scientist
	{
		*cvar:mn_scientists_hired = ( *cvar:mn_scientists_hired + 1 )
		*cvar:mn_scientists_unhired = ( *cvar:mn_scientists_unhired - 1 )
		*cvar:mn_total_scientists = ( *cvar:mn_scientists_hired + *cvar:mn_scientists_unhired )
		*cvar:mn_scientists_available_string = "<cvar:mn_scientists_hired> / <cvar:mn_total_scientists> scientists hired"
	}

	// Clear temp cvars not needed after init
	confunc clear_temp
	{
		delete *cvar:rs_cur_base_itemid
	}

	// Clear all research items and cvars
	confunc clear_all
	{
		// Clear all research items in this base and others
		call *node:root.base_container.research.techs@removeAllChild
		call *node:root.base_container.research_ext.techs_ext@removeAllChild

		// Clear all cvars related to research item details
		if ( exists *cvar:mn_research_image ) { delete *cvar:mn_research_image }
		if ( exists *cvar:mn_research_model ) { delete *cvar:mn_research_model }
		if ( exists *cvar:mn_research_selname ) { delete *cvar:mn_research_selname }
		if ( exists *cvar:mn_research_selstatus ) { delete *cvar:mn_research_selstatus }
		if ( exists *cvar:mn_research_selstatus_long ) { delete *cvar:mn_research_selstatus_long }
		if ( exists *cvar:mn_research_selbase ) { delete *cvar:mn_research_selbase }
		if ( exists *cvar:mn_research_seltime ) { delete *cvar:mn_research_seltime }
		if ( exists *cvar:mn_research_seltimebar ) { delete *cvar:mn_research_seltimebar }
		if ( exists *cvar:mn_research_labs ) { delete *cvar:mn_research_labs }
		if ( exists *cvar:mn_current_labspace ) { delete *cvar:mn_current_labspace }
		if ( exists *cvar:mn_max_labspace ) { delete *cvar:mn_max_labspace }
		if ( exists *cvar:mn_research_scis ) { delete *cvar:mn_research_scis }
		if ( exists *cvar:mn_scientists_available ) { delete *cvar:mn_scientists_available }
		if ( exists *cvar:mn_scientists_hired ) { delete *cvar:mn_scientists_hired }
		if ( exists *cvar:mn_scientists_unhired ) { delete *cvar:mn_scientists_unhired }
	}

	func onWindowOpened
	{
		cmd "research_init;"
		cmd "clear_temp;"

		// Concatenate the string for the lab space
		cmd "get_lab_space_string;"
		cmd "available_scientists;"
	}

	func onWindowClosed
	{
		// Clear all research items and cvars
		cmd "clear_all;"

		// Update values for overall base display
		cmd "base_init;"
	}
}
