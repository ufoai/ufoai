// ==================
// EQUIPMENT MENU
// ==================

component equipSoldier extends panel {
	{
		size			"260 25"
		bgcolor			"0.56 0.81 0.76 0.1"
		onMouseEnter	{ *node:this@bgcolor = "0.56 0.81 0.76 0.2" }
		onMouseLeave	{ *node:this@bgcolor = "0.56 0.81 0.76 0.1" }
	}

	data id {
		string		""
	}

	string name {
		pos				"30 0"
		color			"0.56 0.81 0.76 1"
		size			"285 25"
		contentalign	ALIGN_CL
		onClick {
			cmd "equipment_soldierlist_select <node:parent.id@integer>;"
		}
	}

	data status {
		integer		0
	}

	image assigned {
		pos		"0 0"
		size	"24 24"
		src		"icons/icon_aircraftsml"
		invis	true
	}
}


window equipment extends objectinfo
{
	{
		fill		true
	}

	// Base name and container panel
	BaseContainerPanel base_container
	{
		// Equipment panel
		panel mainBody
		{
			{ size "715 534" }
			// Title bar
			panel header
			{
				{}
				string title
				{
					string			"_Equipment"
				}
			}
			panel soldiers
			{
				{
					pos			"2 40"
					size		"305 490"
				}
				panel soldierlist {
					pos				"0 0"
					size			"285 490"
					padding			6
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
					wheelscrollable	true

					onViewChange {
						*node:parent.soldierlist_scroll@fullsize = <fullsize>
						*node:parent.soldierlist_scroll@current = <viewpos>
						*node:parent.soldierlist_scroll@viewsize = <viewsize>
					}
				}
				vscrollbar soldierlist_scroll {
					image		"ui/scrollbar_v"
					pos			"287 0"
					height		"490"
					hidewhenunused true
					current		0
					viewsize	8
					fullsize	50

					onChange {
						*node:parent.soldierlist@viewpos = <current>
					}
				}
				confunc equipment_soldierlist_clear {
					call *node:parent.soldierlist@removeAllChild
				}
				// <1> Soldier UCN
				// <2> Soldier name
				// <3> Is soldier wounded?
				confunc equipment_soldierlist_add {
					call *node:parent.soldierlist@createChild("member<1>", "equipSoldier")
					*node:parent.soldierlist.member<1>.id@integer = <1>
					*node:parent.soldierlist.member<1>.name@string = <2>
					*node:parent.soldierlist.member<1>.status@integer = <3>
					if ( <3> == 1 ) {
						*node:parent.soldierlist.member<1>.name@color = "1 0.81 0.76 1"
					}
					if ( <4> ne "" ) {
						*node:parent.soldierlist.member<1>.assigned@invis = false
						*node:parent.soldierlist.member<1>.assigned@tooltip = <4>
					}

					if ( *node:root.selected@integer < 0 ) {
						cmd "equipment_soldierlist_select <1>;"
						*node:root.selected@integer = <1>
					}
				}
				confunc equipment_soldierlist_select {
					if ( *node:root.selected@integer >= 0 ) {
						if ( *node:parent.soldierlist.member<node:root.selected@integer>.status@integer == 0 ) {
							*node:parent.soldierlist.member<node:root.selected@integer>.name@color = "0.56 0.81 0.76 1"
						} else {
							*node:parent.soldierlist.member<node:root.selected@integer>.name@color = "1 0.81 0.76 1"
						}
					}
					*node:root.selected@integer = <1>
					*cvar:mn_ucn = <1>
					if ( *node:root.selected@integer >= 0 ) {
						*node:parent.soldierlist.member<1>.name@color = "1 1 1 1"
						*node:root.load_warnings@integer = 0
						cmd "ui_team_select_ucn <node:root.selected@integer>;"
						cmd "mn_updateactorload equip_actorload;"
					}
				}
			} // end soldiers
			// Paperdoll panel
			panel paperdoll
			{
				{
					pos			"310 40"
					size		"400 418"
					bgcolor		"0.56 0.81 0.76 0.1"
					bordercolor	"0.58 0.81 0.758 0.7"
					border		"2"
					onMouseEnter	{
						*node:this@bgcolor = "0.56 0.81 0.76 0.15"
					}
					onMouseLeave	{
						*node:this@bgcolor = "0.56 0.81 0.76 0.1"
					}
				}

				textentry name
				{
					string		*cvar:mn_name
					tooltip		"_Rename your unit"
					pos			"3 0"
					size		"220 32"
					color		"0.56 0.81 0.76 1"
					onChange	{
						cmd "employee_changename;"
						cmd "ui_team_fillequip <node:root.param@string>;"
						cmd "update_item_list;"
						cmd "equipment_soldierlist_select <node:root.selected@integer>;"
					}
				}

				string rank
				{
					string			*cvar:mn_chrrank
					pos				"8 25"
					size			"300 15"
					font			"f_verysmall"
					color			"0.56 0.81 0.76 1"
					longlines		LONGLINES_CHOP
					contentalign	ALIGN_UL
					ghost			true
				}
				/*
				// @TODO: make next/prev soldier btns work
				button next
				{
					icon			"icons/windownext"
					tooltip			"_Next soldier"
					pos				"506 10"
					size			"18	18"
					onClick			{ cmd "nextsoldier;" }
				}
				button prev
				{
					icon			"icons/windowprev"
					tooltip			"_Previous soldier"
					pos				"482 10"
					size			"18	18"
					onClick			{ cmd "prevsoldier;" }
				}*/

				// Inventory area
				image rank_img
				{
					pos 		"10 46"
					size 		"64 64"
					src 		*cvar:mn_chrrank_img
				}
				model actor
				{
					{
						src		*cvar:mn_body
						skin	*cvar:mn_body_skin
						anim	"stand_menu"
						origin	"0 -25 0"
						angles	"0 82 90"
						scale	"5 5 5"
						pos		"86 81"
						size	"191 262"
					}
					model head
					{
						src		*cvar:mn_head
						skin	*cvar:mn_head_skin
						tag		"tag_head"
						scale	"1.0 1.0 1.0"
					}
				}
				image inv_bg
				{
					pos		"10 59"
					size	"351 349"
					src		"/ui/inv_bg_nofloor"
				}

				/**
				 * @sa CL_InitSkin_f
				 * @sa team_initskin
				 */
				selectbox skins {
					pos		"115 59"
					size	"120 40"
					cvar	*cvar:mn_body_skin
					// TODO: ui_team_changeskin is only available in the campaign cgame
					onChange { cmd "ui_team_changeskin <node:root.selected@integer> <cvar:mn_body_skin>;" }
					dataid	OPTION_SINGLEPLAYER_SKINS
				}

				button bt_load
				{
					pos "220 30"
					contentalign ALIGN_CL
					string	"_0/0 Kg"
					color "1 1 1 1"
					flashcolor "1 0 0 1"
					size "285 22"
					onClick { cmd "mn_updateactorload equip_actorload;" }
				}

				// ==================
				// Containers (see inventory.ufo for the definition)
				// ==================

				//container implant	{ pos "13 104"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"} }

				container headgear	{
						pos "88 85"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container backpack	{
						pos "236 59"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container right		{
						pos "13 146"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container left		{
						pos "236 146"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container belt		{
						pos "13 204"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container holster	{
						pos "285 204"
						onSelect { cmd "object_update <lastselectedid> update_item_list;" }
						onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"}
					}

				container armour	{
					pos "130 131"
					color "1 1 1 0"		// active, but display it transparent
					onChange { cmd "equipment_soldierlist_select <node:root.selected@integer>;" }
					onSelect { cmd "object_update <lastselectedid> update_item_list;" }
				}

				confunc equip_actorload {
					*node:parent.bt_load@string = "<1>"
					*node:parent.bt_load@tooltip = "<2>"
					*node:root.load_warnings@integer = "<4>"
					if ( <3> > 0 ) { *node:parent.bt_load@color = "0 1 0 1" }
					elif ( <3> < 0 ) { *node:parent.bt_load@color = "1 1 0 1" }
					else { *node:parent.bt_load@color = "1 1 1 1" }
					if ( <4> > 0 ) { *node:parent.bt_load@flash = true }
					else { *node:parent.bt_load@flash = false }
				}
			}

			MainMenu3Btn bt_deequipsoldier {
				string			"_Remove all"
				pos				"420 475"
				size			"140 30"
				font			"f_small_bold"
				color			"0 0 0 0.9"
				selectcolor		"0 0 0 1"
				onClick	{
					cmd "ui_team_deequip <node:root.selected@integer>;update_item_list;"
					cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"
				}
			}
		}//end mainBody

		// Object description panel
		ObjectInfo details
		{
			{
				pos				"4 574"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
			}
		}

	} // end base_container

	// ==================
	// names list
	// ==================

	data selected {
	}

	data param {
	}
	// ==================
	// category buttons
	// ==================

	confunc update_item_list {
		if ( *cvar:mn_equiptype == 0 ) {
			*node:root.equip@filter = FILTER_S_PRIMARY
		}
		elif ( *cvar:mn_equiptype == 1 ) {
			*node:root.equip@filter = FILTER_S_SECONDARY
		}
		elif ( *cvar:mn_equiptype == 2 ) {
			*node:root.equip@filter = FILTER_S_HEAVY
		}
		elif ( *cvar:mn_equiptype == 3 ) {
			*node:root.equip@filter = FILTER_S_MISC
		}
		elif ( *cvar:mn_equiptype == 4 ) {
			*node:root.equip@filter = FILTER_S_ARMOUR
		}
		elif ( *cvar:mn_equiptype == 5 ) {
			*node:root.equip@filter = FILTER_S_IMPLANT
		}
		*node:root.equip@viewpos = 0
	}

	// The tooltips defined with the options below do not show up. The reason is:
	// A tooltip is displayed only if the option text was truncated.
	// In our case: no text->no truncation->no tooltip.
	// Add a label to the first option and it will be displayed but the last icon will not show up.
	tab equip_type
	{
		{
			pos		"720 10"
			size	"330 32"
			cvar	*cvar:mn_equiptype
			onChange {
				cmd	"update_item_list;"
			}
		}

		option primary {
			icon "icons/icon_primarysml"
			tooltip	"_Primary"
			value	"0"
		}
		option secondary {
			icon "icons/icon_secondarysml"
			tooltip	"_Secondary"
			value	"1"
		}
		option heavy {
			icon "icons/icon_heavysml"
			tooltip	"_Heavy"
			value	"2"
		}
		option misc {
			icon "icons/icon_miscsml"
			tooltip	"_Misc"
			value	"3"
		}
		option armour {
			icon "icons/icon_armoursml"
			tooltip	"_Armour"
			value	"4"
		}
	}

	abilities equip_abilities
	{
		{
			pos			"740 475"
		}
	}
	skillbars equip_skills
	{
		{
			pos			"740 600"
		}
	}
	healthbar equip_health
	{
		{
			pos			"740 720"
		}
	}

	baseinventory equip		{
		pos						"750 50"
		size					"257 400"
		displayweapon			true
		displayammoofweapon		true
		displayunavailableitem	true
		columns					1
		onViewChange {
			*node:root.equip_scroll@current = <viewpos>
			*node:root.equip_scroll@viewsize = <viewsize>
			*node:root.equip_scroll@fullsize = <fullsize>
		}
		onSelect { cmd "object_update <lastselectedid> update_item_list 0;" }
		onChange {
			cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"
			cmd "equipment_soldierlist_select <node:root.selected@integer>;"
		}
	}

	vscrollbar equip_scroll {
		image	"ui/scrollbar_v"
		pos "990 50"	height	"400"
		hidewhenunused true
		onChange {
			*node:root.equip@viewpos = <current>
		}
	}


	data load_warnings {
		integer 0
	}


	// ==================
	// additional stuff
	// ==================

	func onWindowClosed {
		/** @note Do not define action here
		 * this close is edited by the code
		 * pushing this menu
		 */
	}

	func onWindowOpened {
//		call *node:root.actor_stats
		*cvar:mn_infotype = 0
		*cvar:mn_equiptype = 0
		*cvar:mn_linkname = ""
		*cvar:mn_itemname = ""
		*node:root.selected@integer = -1
		*node:root.param@string = ""

		cmd "team_initskin;"
		if ( paramCount > 0 ) {
			cmd "ui_team_fillequip <1>;"
			*node:root.param@string = <1>
		} else {
			cmd "ui_team_fillequip;"
		}
		cmd "update_item_list;"
	}
	// Back to geoscape
	Geoscape geoscape_button
	{
		onClick			{ cmd "ui_pop; ui_pop;" }
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }

}
