// ====================================
// Battlescape Hud Assets
// ====================================

// Circular button background
component CircleBtn extends panel
{
	{
		size	"48 48"
		onMouseEnter 	{ *node:this.CircleBtn_halo@invis = false }
		onMouseLeave 	{ *node:this.CircleBtn_halo@invis = true }
	}

	// Mouseover effect
	button CircleBtn_halo
	{
		icon	"icons/circle_halo"
		pos		"0 0"
		size	"48 48"
		invis	true
		ghost	true
	}
}

// Disabled icon
component CircleBtnDisabled extends button
{
	size		"48 48"
	icon		"icons/circle_disabled0"
	invis		true
}

// Soldier head for soldier selection
component SoldierSelect extends panel
{
	{
		size			"40 40"
		bgcolor			"0 0.1 0 0.7"
		border			"2"
		bordercolor		"0.527 0.6 0.21 1"
		onMouseEnter	{ *node:this@bgcolor = "0 0 0 1" }
		onMouseLeave	{ *node:this@bgcolor = "0 0.1 0 0.7" }
		onClick			{ cmd "actor_select <node:this@num>;" }
	}
	model head
	{
		angles		"15 60 100"
		autoscale	true
		pos			"1 1"
		size		"38 34"
		ghost 		true
	}
	texture bar_bg
	{
		pos			"1 34"
		size		"38 4"
		src			"ui/bar_bg"
		bgcolor		"0 0 0 0.5"
		ghost		true
	}
	bar health
	{
		pos			"1 34"
		size		"38 4"
		color		".1875 .65625 .164 1"
		direction	"cr"
		disabled	true
		ghost		true
		nohover		true
	}
	bar tu
	{
		pos			"1 34"
		size		"38 4"
		color		".582 .808 .758 1"
		direction	"cr"
		disabled	true
		invis		true
		ghost		true
		nohover		true
	}
	bar morale
	{
		pos			"1 34"
		size		"38 4"
		color		".825 .92 .437 1"
		direction	"cr"
		disabled	true
		invis		true
		ghost		true
		nohover		true
	}
}

component WorldLevel extends panel
{
	{
		size		"100 40"
		ghost		true
	}

	button icon
	{
		pos		"4 3"
		size	"19 19"
		icon	"icons/levels"
		tooltip	"_current level"
	}
	string levelnum
	{
		string			1
		pos				"27 3"
		size			"30 25"
		font			"f_normal_bold"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_CL
		ghost			true
	}

	button up
	{
		pos		"57 2"
		size	"18 10"
		icon	"icons/arrow_up"
		tooltip	"_raise the level"
		onclick	{ cmd "lvlchange 1;" }
	}

	button down
	{
		pos		"57 16"
		size	"18 10"
		icon	"icons/arrow_down"
		tooltip	"_lower the level"
		onclick	{ cmd "lvlchange -1;" }
	}

	// raise/lower the level
	// param1: direction (0 = down, 1 = up)
	confunc lvlchange
	{
		if (<1> == 1) {
			if (*cvar:cl_worldlevel < *cvar:cl_maxworldlevel) {
				*cvar:cl_worldlevel = (*cvar:cl_worldlevel + 1)
			}
		} elif (<1> == -1) {
			if (*cvar:cl_worldlevel > "0") {
				*cvar:cl_worldlevel = (*cvar:cl_worldlevel - 1)
			}
		}
		*cvar:i = (*cvar:cl_worldlevel + 1)
		*node:parent.levelnum@string = <cvar:i>
		delete *cvar:i
		// change icon if we're at top or bottom level
		if (*cvar:cl_worldlevel eq "0") {
			*node:parent.down@icon = "icons/arrow_down_off"
			*node:parent.up@icon = "icons/arrow_up"
		} elif (*cvar:cl_worldlevel eq *cvar:cl_maxworldlevel) {
			*node:parent.up@icon = "icons/arrow_up_off"
			*node:parent.down@icon = "icons/arrow_down"
		} else {
			*node:parent.down@icon = "icons/arrow_down"
			*node:parent.up@icon = "icons/arrow_up"
		}
	}
}

component cmpWound extends panel
{
	{
		size		"180 16"
	}

	button wound_icon {
		color	"1 0 0 1"
		size	"16 16"
	}

	string wound_string {
		pos		"20 0"
		font		"f_small"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_UL
	}
}

component ReactionFireShooter extends panel
{
	{
		size	"210 35"
		bgcolor "0 0 0 0.7"
		border	1
		bordercolor		"1 1 1 0.2"
	}
	model head_shooter
	{
		src		""
		skin	""
		angles	"0 60 90"
		autoscale	true
		pos		"5 5"
		size	"25 25"
		origin	"0 0 0"
		ghost	true
	}
	bar timeremaining
	{
		pos		"35 22"
		size	"170 8"
		current	0
		max		0
		disabled	true
		border	1
		bordercolor	"1 1 1 0.2"
		ghost	true
	}
	string rfnotice
	{
		string	""
		size	"170 17"
		pos		"35 5"
		ghost	true
		font	"f_verysmall"
	}
}
