// ======================
// NEW HUD UNNAMED SO FAR
// ======================

/*
 * @TODO
 * - No ammo models/bar because cvars don't exist
 * - Some way to see which ammo is loaded
 * - Currently selected firemode name on weapon panel
 * - Second weapon panel
 * -- Reload button disappears when full, but it would
 *	be great to have full/not-full/empty icons
 * -- Need to figure out grenades, and sometimes pistols
 *	they say can't be fired two-handed, and reload btn
 *	doesn't appear. Try to figure out when this happens.
 * - Fire weapon seems off: sometimes no line of fire is drawn
 * - Soldier models don't always update
 * - Inventory/Stats windows
 * -- Turn it into panel?
 * - Chat windows, and other MP-specific needs
 * - Message panel (log lines use old style)
 * - Show keys
 * - Level selection needs up/down arrows
 * - Tooltips: look for UI_DrawTooltip
 * - Rescue zone button
 */

window hud_default extends hud
{
	{
		fill		true
		starlayout	true
		noticepos	"512 50"
	}

	func onScriptLoaded {
		// Add the window to the list of HUDs
		cmd "add_hud_name <path:root> \"_Default\""
	}

	battlescape render
	{
		pos			"0 0"
		size		"1024 768"
		align		LAYOUTALIGN_FILL
	}

	// ============================
	// reaction fire tracking panel
	// parameter to display the panel
	// ============================
	panel reactions
	{
		{
			pos		"122 62"
			size		"210 500"
			layout		LAYOUT_TOP_DOWN_FLOW
			layoutMargin	4
			ghost		true
		}
	}

	// ==============
	// SOLDIERS PANEL
	// ==============
	panel soldier
	{
		{
			pos			"0 418"
			size		"1024 220"
			align		LAYOUTALIGN_BOTTOMLEFT
			ghost		true
		}

		panel soldier_opts
		{
			{
				pos			"10 130"
				size		"780 76"
//				bgcolor		"0.38 0.48 0.36 0.5"
				bgcolor		"0 0.15 0.1 0.7"
				bordercolor	"0.58 0.81 0.758 0.7"
				border		"2"
				onMouseEnter	{
					*node:this@bgcolor = "0 0.21 0.158 0.7"
				}
				onMouseLeave	{
					*node:this@bgcolor = "0 0.15 0.1 0.7"
				}
			}

			// Soldier name
			string sel_soldier_name
			{
				string			*cvar:mn_name
				pos				"105 6"
				size			"300 15"
				font			"f_small_bold"
				color			"0.56 0.81 0.76 1"
				longlines		LONGLINES_CHOP
				contentalign	ALIGN_CL
				ghost			true
			}

			// Soldier status bars
			panel soldier_status
			{
				{
					pos		"105 18"
					size	"355 60" // 420
					ghost	true
				}

				// Health bar
				texture health_bg
				{
					pos			"0 5"
					size		"300 10"
					src			"ui/bar_bg"
					ghost		true
				}
				bar health
				{
					pos			"0 5"
					size		"300 10"
					color		".1875 .65625 .164 1"
					current		*cvar:mn_hp
					max			*cvar:mn_hpmax
					disabled	true
					nohover		true
				}
				bar stun
				{
					pos			"0 6"
					size		"300 8"
					color		".3 .3 .3 .9"
					current		*cvar:mn_stun
					max			*cvar:mn_hpmax
					disabled	true
					nohover		true
				}
				string health_count
				{
					string			*cvar:mn_hp
					pos				"305 4"
					size			"30	12"
					font			"f_verysmall_bold"
					color			"0.56 0.81 0.76 0.5"
					contentalign	ALIGN_CL
				}
				button health_icon
				{
					icon			"icons/healthsml0"
					pos				"335 0"
					size			"18	19"
				}

				// Time units bar
				texture tu_bg
				{
					pos			"0 22"
					size		"300 10"
					src			"ui/bar_bg"
					ghost		true
				}
				bar tu
				{
					pos			"0 22"
					size		"300 10"
					color		".582 .808 .758 1"
					current		*cvar:mn_tu
					max			*cvar:mn_tumax
					disabled	true
					nohover		true
				}
				bar tureserved
				{
					pos			"0 23"
					size		"300 8"
					color		"0 0 0 .3"
					current		*cvar:mn_tureserved
					max			*cvar:mn_tumax
					disabled	true
					nohover		true
				}
				string tu_count
				{
					string			*cvar:mn_tu
					pos				"305 21"
					size			"30	12"
					font			"f_verysmall_bold"
					color			"0.56 0.81 0.76 0.5"
					contentalign	ALIGN_CL
				}
				button tu_icon
				{
					icon			"icons/tusml0"
					pos				"335 17"
					size			"18	19"
				}

				// Morale bar
				texture morale_bg
				{
					pos			"0 39"
					size		"300 10"
					src			"ui/bar_bg"
					ghost		true
				}
				bar morale
				{
					pos			"0 39"
					size		"300 10"
					color		".825 .92 .437 1"
					current		*cvar:mn_morale
					max			*cvar:mn_moralemax
					disabled	true
					nohover		true
				}
				string morale_count
				{
					string			*cvar:mn_morale
					pos				"305 38"
					size			"30	12"
					font			"f_verysmall_bold"
					color			"0.56 0.81 0.76 0.5"
					contentalign	ALIGN_CL
				}
				button morale_icon
				{
					icon			"icons/moralesml0"
					pos				"335 34"
					size			"18	19"
				}
			}

			// Soldier buttons
			// @TODO: inventory button should stay "on" when window is open
			// @TODO: stats button should stay "on" when window is open
			panel soldier_buttons
			{
				{
					pos			"470 13"
					size		"310 50"
					ghost		true
				}

				// Inventory button
				CircleBtn inventory_bg
				{
					{
						pos			"0 0"
					}

					button icon
					{
						icon		"icons/inv0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip		"_Equipment"
						onClick		{
									//cmd "toggle_button inventory_bg inv;"
									cmd "show_inventory;"
								}
						onMouseEnter	{ *node:this@icon = "icons/inv" }
						onMouseLeave	{ *node:this@icon = "icons/inv0" }
					}
				}

				confunc show_inventory { cmd "show_hide_inv_stats;" }

				// Soldier stats button
				CircleBtn stats_bg
				{
					{
						pos			"55 0"
					}

					button icon
					{
						icon		"icons/stats0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip		"_Stats"
						onClick		{
									//cmd "toggle_button stats_bg stats;"
									cmd "show_physdat;"
								}
						onMouseEnter	{ *node:this@icon = "icons/stats" }
						onMouseLeave	{ *node:this@icon = "icons/stats0"	}
					}
				}

				confunc show_physdat { cmd "show_hide_inv_stats;" }

				// Headgear button
				CircleBtn headgear_bg
				{
					{
						pos		"110 0"
					}

					button icon
					{
						icon		"icons/headgear0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip		"_Headgear"
						onMouseEnter	{ *node:this@icon = "icons/headgear" }
						onMouseLeave	{ *node:this@icon = "icons/headgear0" }
						onClick	{ cmd "actor_useheadgear;" }
					}

					CircleBtnDisabled headgear_d
					{
						pos			"0 0"
					}

					confunc deselect_headgear
					{
						*node:parent.headgear_d@invis = true
					}
					confunc disable_headgear
					{
						*node:parent.headgear_d@invis = false
					}
				}

				// Reaction fire button
				CircleBtn reaction
				{
					{
						pos			"165 0"
					}

					button icon_on
					{
						icon		"icons/reactionon0"
						background	"icons/circle"
						pos			"0 0"
						size		"48 48"
						invis		true
						tooltip			"_Toggle reaction fire."
						onMouseEnter	{ *node:this@icon = "icons/reactionon" }
						onMouseLeave	{ *node:this@icon = "icons/reactionon0" }
						onClick		{ cmd "deselect_reaction; hud_togglereaction;" }
					}

					button icon_off
					{
						icon		"icons/reactionoff0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip			"_Toggle reaction fire."
						onMouseEnter	{ *node:this@icon = "icons/reactionoff" }
						onMouseLeave	{ *node:this@icon = "icons/reactionoff0" }
						onClick		{ cmd "startreaction; hud_togglereaction;" }
					}

					// Disable icon over reaction button
					button reaction_d
					{
						pos			"0 0"
						size		"48 48"
						icon		"icons/circle_disabled0"
						invis		true
					}
				}

				// Stand button when not selected
				CircleBtn stand_off
				{
					{
						pos			"220 0"
					}

					button icon
					{
						icon		"icons/stand0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip		"_Stand"
						onClick		{ cmd "actor_standcrouch;" }
						onMouseEnter	{ *node:this@icon = "icons/stand" }
						onMouseLeave	{ *node:this@icon = "icons/stand0"	}
					}
				}

				// Crouch button when not selected
				CircleBtn crouch_off
				{
					{
						pos			"250 0"
					}

					button icon
					{
						icon		"icons/crouch0"
						background	"icons/circle0"
						pos			"0 0"
						size		"48 48"
						tooltip		"_Crouch"
						onClick		{ cmd "actor_standcrouch;" }
						onMouseEnter	{ *node:this@icon = "icons/crouch" }
						onMouseLeave	{ *node:this@icon = "icons/crouch0"	}
					}
				}

				// Crouch button when selected
				CircleBtn crouch
				{
					{
						pos			"250 0"
						tooltip		"_Crouched"
						invis		true
					}

					button icon
					{
						icon		"icons/crouch"
						background	"icons/circle"
						pos			"0 0"
						size		"48 48"
						ghost		true
					}
				}

				// Disable icon over crouch button
				button crouch_d
				{
					pos			"250 0"
					size		"48 48"
					icon		"icons/circle_disabled0"
					invis		true
				}

				// Stand button when selected
				CircleBtn stand
				{
					{
						pos			"220 0"
						tooltip		"_Standing"
						invis		false
					}

					button icon
					{
						icon		"icons/stand"
						background	"icons/circle"
						pos			"0 0"
						size		"48 48"
						ghost		true
					}
				}

				// Disable icon over stand button
				button stand_d
				{
					pos			"220 0"
					size		"48 48"
					icon		"icons/circle_disabled0"
					invis		true
				}

				// Sets the proper display for the stand/crouch buttons
				confunc deselect_stand
				{
					*node:parent.stand@invis = false
					*node:parent.crouch@invis = true
					*node:parent.stand_d@invis = true
					*node:parent.crouch_d@invis = true
				}
				confunc deselect_crouch
				{
					*node:parent.stand@invis = true
					*node:parent.crouch@invis = false
					*node:parent.stand_d@invis = true
					*node:parent.crouch_d@invis = true
				}
				// Disables the stand/crouch buttons when no TUs remain
				confunc disable_stand
				{
					*node:parent.stand@invis = false
					*node:parent.crouch@invis = false
					*node:parent.stand_d@invis = false
					*node:parent.crouch_d@invis = false
				}
				confunc disable_crouch
				{
					*node:parent.stand@invis = false
					*node:parent.crouch@invis = false
					*node:parent.stand_d@invis = false
					*node:parent.crouch_d@invis = false
				}
			}
		}

		// Right-hand weapon panel
		// @TODO implement firemode selection
		// @TODO add button to show left-hand weapon panel
		panel weapon
		{
			{
				pos			"800 130"
				size		"206 76"
//				bgcolor		"0.38 0.48 0.36 0.5"
				bgcolor		"0 0.15 0.1 0.7"
				bordercolor	"0.58 0.81 0.758 0.7"
				border		"2"
				onClick			{ cmd "hud_fireweapon r 0;" }
				onMouseEnter	{ *node:this@bgcolor = "0 0.21 0.158 0.7" }
				onMouseLeave	{ *node:this@bgcolor = "0 0.15 0.1 0.7"	}
			}
			container right
			{
				pos		"10 10"
			}
			container left
			{
				invis	true
				pos		"10 10"
			}
			/*
			string fmode_label
			{
				string 			*cvar:fm_selected
				pos				"5 60"
				size			"196 10"
				font			"f_small_bold"
				color			"0.58 0.81 0.758 1"
				contentalign	ALIGN_CR
				longlines		LONGLINES_CHOP
				ghost			true
			}
			*/
			button fire_r
			{
				pos		"5 5"
				size	"35 35"
				invis	false
				icon	"icons/fire0"
				onClick			{ cmd "ui_push_dropdown firemode_right.anchor LAYOUTALIGN_BOTTOMLEFT hud_default.soldier.weapon LAYOUTALIGN_TOPLEFT;" }
				onMouseEnter	{ *node:this@icon = "icons/fire" }
				onMouseLeave	{ *node:this@icon = "icons/fire0" }
			}
			button fire_l
			{
				pos		"5 5"
				size	"35 35"
				invis	true
				icon	"icons/fire0"
				onClick			{ cmd "ui_push_dropdown firemode_left.anchor LAYOUTALIGN_BOTTOMLEFT hud_default.soldier.weapon LAYOUTALIGN_TOPLEFT;" }
				onMouseEnter	{ *node:this@icon = "icons/fire" }
				onMouseLeave	{ *node:this@icon = "icons/fire0" }
			}
			button reload_r
			{
				pos				"173 5"
				size			"28 23"
				invis			false
				icon			"icons/reload0"
				onMouseEnter	{
					*node:this@icon = "icons/reload"
					cmd "hud_remainingtus reload_r 1;"
				}
				onMouseLeave	{
					*node:this@icon = "icons/reload0"
					cmd "hud_remainingtus reload_r 0;"
				}
				onClick			{ cmd "hud_reloadright;" }
			}
			button reload_r_disabled
			{
				pos				"173 5"
				size			"28 23"
				invis			true
				icon			"icons/reload_disabled"
				onClick			{ cmd "hud_reloadright;" }
			}
			button reload_l
			{
				pos				"173 5"
				size			"28 23"
				invis			true
				icon			"icons/reload0"
				onMouseEnter {
					*node:this@icon = "icons/reload"
					cmd "hud_remainingtus reload_l 1;"
				}
				onMouseLeave {
					*node:this@icon = "icons/reload0"
					cmd "hud_remainingtus reload_l 1;"
				}
				onClick			{ cmd "hud_reloadleft;" }
			}
			button reload_l_disabled
			{
				pos				"173 5"
				size			"28 23"
				invis			true
				icon			"icons/reload_disabled"
				onClick			{ cmd "hud_reloadleft;" }
			}
			string ammo_count
			{
				string			*cvar:mn_ammoright
				pos				"130 18"
				size			"40 10"
				font			"f_small_bold"
				color			"0.58 0.81 0.758 1"
				contentalign	ALIGN_CR
				ghost			true
			}
			model oweapon
			{
				src			*cvar:mn_lweapon
				angles		"-15 170 90"
				scale		"1.8 1.8 1.8"
				tooltip		"_View other hand"
				pos			"130 33"
				size 		"72 39"
				bgcolor			"0 0.1 0 0.5"
				border			"2"
				bordercolor		"0.527 0.6 0.21 1"
				onMouseEnter	{
					*node:this@bgcolor = "0 0.1 0 0.9"
					*node:this@bordercolor = "0.824 0.917 0.437 1"
				}
				onMouseLeave	{
					*node:this@bgcolor = "0 0.1 0 0.5"
					*node:this@bordercolor = "0.527 0.6 0.21 1"
				}
				onClick			{ call *node:root.soldier.weapon.switchhands }
			}

			// Displays oweapon only if another weapon exists
			confunc show_oweapon
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "r") {
					if (*cvar:mn_lweapon eq "") {
						*node:parent.oweapon@invis = true
					} else {
						*node:parent.oweapon@invis = false
					}
				call *node:parent.show_rhand
				} else {
					if (*cvar:mn_rweapon eq "") {
						*node:parent.oweapon@invis = true
					} else {
						*node:parent.oweapon@invis = false
					}
				call *node:parent.show_lhand
				}
			}
			func show_lhand {
					*node:parent.right@invis = true
					*node:parent.left@invis = false
					*node:parent.fire_r@invis = true
					*node:parent.fire_l@invis = false
					*node:parent.reload_r@invis = true
					*node:parent.reload_r_disabled@invis = true
					*node:parent.oweapon@src = *cvar:mn_rweapon
					*node:parent.ammo_count@string = *cvar:mn_ammoleft

			}
			func show_rhand {
					*node:parent.right@invis = false
					*node:parent.left@invis = true
					*node:parent.fire_r@invis = false
					*node:parent.fire_l@invis = true
					*node:parent.reload_l@invis = true
					*node:parent.reload_l_disabled@invis = true
					*node:parent.oweapon@src = *cvar:mn_lweapon
					*node:parent.ammo_count@string = *cvar:mn_ammoright

			}
			func switchhands
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "r") {
					*cvar:sldcurhand<cvar:cl_selected> = "l"
					call *node:parent.show_lhand
				} else {
					*cvar:sldcurhand<cvar:cl_selected> = "r"
					call *node:parent.show_rhand
				}
			}
			confunc deselect_primaryleft
			{
				*node:parent.fire_l@invis = false
			}
			confunc deselect_primaryright
			{
				*node:parent.fire_r@invis = false
			}
			confunc deselect_reloadleft
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "l") {
					*node:parent.reload_l@invis = false
					*node:parent.reload_l_disabled@invis = true
				}
			}
			confunc deselect_reloadright
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "r") {
					*node:parent.reload_r@invis = false
					*node:parent.reload_r_disabled@invis = true
				}
			}
			confunc disable_primaryleft { *node:parent.fire_l@invis = true }
			confunc disable_primaryright { *node:parent.fire_r@invis = true }
			confunc disable_reloadleft
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "l") {
					*node:parent.reload_l@invis = true
					*node:parent.reload_l_disabled@invis = false
				}
			}
			confunc disable_reloadright
			{
				if (*cvar:sldcurhand<cvar:cl_selected> eq "r") {
					*node:parent.reload_r@invis = true
					*node:parent.reload_r_disabled@invis = false
				}
			}
		}

		// Soldier selection
		panel squad
		{
			{
				pos			"105 56"
				size		"565 68"
				invis		false
				ghost		true
			}
			button squadtoggle
			{
				icon		"icons/windowprev"
				tooltip		"_Hide squad"
				pos			"10 0"
				size		"18 18"
				onClick			{
					*node:root.soldier.squadtoggle@invis = false
					*node:parent@invis = true
				}
			}
			button health_btn
			{
				pos		"34 0"
				size	"18 19"
				icon	"icons/healthsml"
				onMouseEnter	{ *node:this@icon = "icons/healthsml" }
				onMouseLeave	{ cmd "squad_hoveroff health;" }
				onClick			{ cmd "sel_squad_panel health;" }
			}
			button tu_btn
			{
				pos		"54 0"
				size	"18 19"
				icon	"icons/tusml0"
				onMouseEnter	{ *node:this@icon = "icons/tusml" }
				onMouseLeave	{ cmd "squad_hoveroff tu;" }
				onClick			{ cmd "sel_squad_panel tu;" }
			}
			button morale_btn
			{
				pos		"74 0"
				size	"18 19"
				icon	"icons/moralesml0"
				onMouseEnter	{ *node:this@icon = "icons/moralesml" }
				onMouseLeave	{ cmd "squad_hoveroff morale;" }
				onClick			{ cmd "sel_squad_panel morale;" }
			}

			// Select which attribute to show on squad panel
			// param1 = attribute
			confunc sel_squad_panel
			{
				// Set a cvar with the currently selected option
				*cvar:squad_bar = <1>

				// Remove any selected buttons
				*node:parent.health_btn@icon = "icons/healthsml0"
				*node:parent.tu_btn@icon = "icons/tusml0"
				*node:parent.morale_btn@icon = "icons/moralesml0"

				// Update the bars with the new selection and highlight
				// the correct button
				*cvar:i = 0;
				if (param1 eq "health") {
					*node:parent.health_btn@icon = "icons/healthsml"
					while ( *cvar:i < 12 ) {
						*node:parent.sld<cvar:i>.health@invis = false
						*node:parent.sld<cvar:i>.tu@invis = true
						*node:parent.sld<cvar:i>.morale@invis = true
						*cvar:i = ( *cvar:i + 1 )
					}
				} elif (param1 eq "tu") {
					*node:parent.tu_btn@icon = "icons/tusml"
					while ( *cvar:i < 12 ) {
						*node:parent.sld<cvar:i>.health@invis = true
						*node:parent.sld<cvar:i>.tu@invis = false
						*node:parent.sld<cvar:i>.morale@invis = true
						*cvar:i = ( *cvar:i + 1 )
					}
				} else {
					*node:parent.morale_btn@icon = "icons/moralesml"
					while ( *cvar:i < 12 ) {
						*node:parent.sld<cvar:i>.health@invis = true
						*node:parent.sld<cvar:i>.tu@invis = true
						*node:parent.sld<cvar:i>.morale@invis = false
						*cvar:i = ( *cvar:i + 1 )
					}
				}
				delete *cvar:i
			}

			// Stop mouseover effect from turning off the button
			// selecting which attribute is display in soldier squad panel
			// param1 = attribute to check
			confunc squad_hoveroff
			{
				if (*cvar:squad_bar ne param1) {
					*node:parent.<1>_btn@icon = "icons/<1>sml0"
				}
			}

			// Soldiers
			SoldierSelect sld0
			{
				pos "12 22"
			}
			SoldierSelect sld1
			{
				pos "58 22"
			}
			SoldierSelect sld2
			{
				pos "104 22"
			}
			SoldierSelect sld3
			{
				pos "150 22"
			}
			SoldierSelect sld4
			{
				pos "196 22"
			}
			SoldierSelect sld5
			{
				pos "242 22"
			}
			SoldierSelect sld6
			{
				pos "288 22"
			}
			SoldierSelect sld7
			{
				pos "334 22"
			}
			SoldierSelect sld8
			{
				pos "380 22"
			}
			SoldierSelect sld9
			{
				pos "426 22"
			}
			SoldierSelect sld10
			{
				pos "472 22"
			}
			SoldierSelect sld11
			{
				pos "518 22"
			}
		}
		// Squad panel toggle button
		button squadtoggle
		{
			icon		"icons/windownext"
			tooltip		"_Show squad"
			pos			"115 106"
			size		"18 18"
			invis		true
			onClick			{
				*node:parent.squad@invis = false
				*node:this@invis = true
			}
		}

		// Soldier model
		model sel_soldier_model
		{
			{
				src			*cvar:mn_body
				skin		*cvar:mn_body_skin
				anim		*cvar:mn_anim
				angles		"0 80 90"
				pos			"-20 0"
				size		"160 220"
				scale		"5 5 5"
				ghost		true
			}
			model sel_soldier_head
			{
				src			*cvar:mn_head
				skin		*cvar:mn_head_skin
				tag			"tag_head"
			}
			model sel_soldier_rweapon
			{
				src			*cvar:mn_rweapon
				tag			"tag_rweapon"
			}
			model sel_soldier_lweapon
			{
				src			*cvar:mn_lweapon
				tag			"tag_lweapon"
			}
		}
	}

	// Alien and civilian counts; Level selection
	panel battle_info
	{
		{
			pos			"0 0"
			size		"512 50"
			align		LAYOUTALIGN_TOPLEFT
			ghost		true
		}
		panel alien_count
		{
			{
				pos			"10 10"
				size		"80 25"
				ghost		true
			}
			button alienhead
			{
				pos		"5 3"
				size	"17 19"
				icon	"icons/alienhead"
				tooltip	"_Jump to alien"
				onClick	{ cmd "nextalien;" }
			}
			string aliencount
			{
				string			*cvar:mn_numaliensspotted
				pos				"27 3"
				size			"50 25"
				font			"f_normal_bold"
				color			"0.56 0.81 0.76 1"
				longlines		LONGLINES_CHOP
				contentalign	ALIGN_CL
				ghost			true
			}
		}
		/* @TODO: add civilian count when we have a cvar
		panel civ_count
		{
			{
				pos			"10 40"
				size		"100 25"
				ghost		true
			}
			button civhead
			{
				pos		"5 3"
				size	"17 19"
				icon	"icons/civhead"
				tooltip	"_Jump to civilian"
			}
			string civcount
			{
				string			"_*"
				pos				"27 3"
				size			"50 25"
				font			"f_normal_bold"
				color			"0.56 0.81 0.76 1"
				longlines		LONGLINES_CHOP
				contentalign	ALIGN_CL
				ghost			true
			}
		}
		*/
		WorldLevel level
		{
			{
				pos			"90 10"
			}
		}
	}

	// End turn, options, abort mission, radar
	panel game_controls
	{
		{
			pos			"0 0"
			size		"200 500"
			align		LAYOUTALIGN_TOPRIGHT
			ghost		true
		}

		// End turn button
		CircleBtn end_turn
		{
			{
				pos			"145 5"
			}

			button icon
			{
				icon		"icons/dblarrowrgt0"
				background	"icons/circle0"
				pos			"0 0"
				size		"48 48"
				tooltip		"_End Turn"
				onClick			{ cmd "nextround;" }
				onMouseEnter	{ *node:this@icon = "icons/dblarrowrgt" }
				onMouseLeave	{ *node:this@icon = "icons/dblarrowrgt0"	}
			}
		}

		// Options button
		CircleBtn options
		{
			{
				pos			"145 60"
			}

			button icon
			{
				icon		"icons/options0"
				background	"icons/circle0"
				pos			"0 0"
				size		"48 48"
				tooltip		"_Options"
				onClick			{ cmd "ui_push options;" }
				onMouseEnter	{ *node:this@icon = "icons/options" }
				onMouseLeave	{ *node:this@icon = "icons/options0"	}
			}
		}

		// Abort button
		CircleBtn abort
		{
			{
				pos			"145 115"
			}

			button icon
			{
				icon		"icons/abortmission0"
				background	"icons/circle0"
				pos			"0 0"
				size		"48 48"
				tooltip		"_Abort"
				onClick		{ cmd "ui_push missionoptions;" }
				onMouseEnter	{ *node:this@icon = "icons/abortmission" }
				onMouseLeave	{ *node:this@icon = "icons/abortmission0"	}
			}
		}

		// Radar button
		CircleBtn radar
		{
			{
				pos			"145 170"
			}

			button icon
			{
				icon		"icons/radar0"
				background	"icons/circle0"
				pos			"0 0"
				size		"48 48"
				tooltip		"_Radar"
				onClick	{ cmd "ui_push radar;" }
				onMouseEnter	{ *node:this@icon = "icons/radar" }
				onMouseLeave	{ *node:this@icon = "icons/radar0"	}
			}
		}
		// Show rescue button
		CircleBtn show_rescue
		{
			{
				pos			"145 225"
			}

			button icon
			{
				icon		"icons/rescue0"
				background	"icons/circle0"
				pos			"0 0"
				size		"48 48"
				tooltip		"_Show rescue zone"
				onClick			{ cmd "toggle_rescuezone;" }
				onMouseEnter	{ *node:this@icon = "icons/rescue" }
				onMouseLeave	{ *node:this@icon = "icons/rescue0"	}
			}
		}

		// Chat window
		// @TODO: Add button to show chat
		// @TODO: To make it appear on MP-only, use: visiblewhen	"*cvar:sv_maxclients >= 2"
	}

	// ==========================
	// Reaction fire panel events
	// ==========================

	/**
	 * @brief Adds a new possible target to the list of reaction fire targets for the given shooter
	 * @param <1> shooter entity number
	 * @param <2> target entity number
	 * @param <3> shooter head model
	 * @param <4> shooter head skin
	 * @param <5> boolean whether shooter is a robot
	 * @param <6> TUs left until rf is triggered
	 * @param <7> Out of range = 1, in range = 0
	 * @param <8> Name of the selected firedef of the shooter
	 */
	confunc reactionfire_addtarget {
		call *node:root.reactions@createChild("rf-<1>-<2>", "ReactionFireShooter")
		*node:root.reactions.rf-<1>-<2>.head_shooter@src = "<3>"
		*node:root.reactions.rf-<1>-<2>.head_shooter@skin = "<4>"
		if ( <5> == 1 ) {
			// top down view for robots - sideview of the body isn't very nice
			*node:root.reactions.rf-<1>-<2>.head_shooter@angles = "0 60 90"
		}

		if ( <7> == 1 ) {
			// Target is out of range
			*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.65 0.13 0 1"
			*node:root.reactions.rf-<1>-<2>.rfnotice@string = "_Target out of range"
		} else {
			if ( <6> <= 0 ) {
				// Turn the panel red if the unit is ready to fire
				*node:root.reactions.rf-<1>-<2>@bgcolor = "0.65 0.13 0 1"
				*node:root.reactions.rf-<1>-<2>.rfnotice@string = "_Target acquired"
			} elif ( <6> < 5 ) {
				// Turn the bar yellow if the unit is nearing fire capability
				*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.83 0.92 0.44 1"
			} else {
				// Otherwise the bar should be blue
				*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.1 0.1 0.7 1"
			}
			*node:root.reactions.rf-<1>-<2>.rfnotice@string = "<8> (<6> TUs)"
		}
		*node:root.reactions.rf-<1>-<2>.timeremaining@max = <6>
	}

	/**
	 * @brief The target is no longer in the reaction fire scope of the shooter
	 * @param <1> shooter entity number
	 * @param <2> target entity number
	 */
	confunc reactionfire_removetarget {
		//call *node:root.reactions.rf-<1>-<2>@deletetimed(1500)
		call *node:root.reactions.rf-<1>-<2>@delete
	}

	/**
	 * @brief Called when the amount of TUs that are needed to trigger the reaction fire changed.
	 * This might e.g. happen if the target is moving in front of the shooter.
	 * @param <1> shooter entity number
	 * @param <2> target entity number
	 * @param <3> TUs left until rf is triggered
	 * @param <4> Out of range = 1, in range = 0
	 */
	confunc reactionfire_updatetarget {
		*node:root.reactions.rf-<1>-<2>.timeremaining@current = (*node:root.reactions.rf-<1>-<2>.timeremaining@max - <3>)

		if ( <4> == 1 ) {
			// Target is out of range
			*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.65 0.13 0 1"
			*node:root.reactions.rf-<1>-<2>.rfnotice@string = "_Target out of range"
		} else {
			if ( <3> <= 0 ) {
				// Turn the panel red if the unit is ready to fire
				*node:root.reactions.rf-<1>-<2>@bgcolor = "0.65 0.13 0 1"
				*node:root.reactions.rf-<1>-<2>.rfnotice@string = "_Target acquired"
			} elif ( <3> < 5 ) {
				// Turn the bar yellow if the unit is nearing fire capability
				*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.83 0.92 0.44 1"
			} else {
				// Otherwise the bar should be blue
				*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.1 0.1 0.7 1"
			}
		}
	}

	/**
	 * @brief Called when the reaction fire has been aborted.
	 * This might e.g. happen if the chance to hit the target is too low.
	 * @param <1> shooter entity number
	 * @param <2> target entity number
	 */
	confunc reactionfire_shotaborted {
		*node:root.reactions.rf-<1>-<2>@bgcolor = "0.65 0.13 0 1"
		*node:root.reactions.rf-<1>-<2>.timeremaining@color = "0.65 0.13 0 1"
		*node:root.reactions.rf-<1>-<2>.rfnotice@string = "_Aborting fire!"
	}

	// Resets the reaction fire button when new soldier is selected
	confunc startreaction
	{
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.icon_off@invis = true
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.icon_on@invis = false
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.reaction_d@invis = true
	}
	confunc deselect_reaction
	{
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.icon_off@invis = false
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.icon_on@invis = true
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.reaction_d@invis = true
	}
	confunc disable_reaction
	{
		*node:parent.soldier.soldier_opts.soldier_buttons.reaction.reaction_d@invis = false
	}

	// Handles turning a button "on" when clicked
	// param1 = path to node from parent
	// param2 = icon image
	confunc toggle_button
	{
		if (*node:parent.soldier.soldier_opts.soldier_buttons.<1>.icon@background eq "icons/circle0") {
			*node:parent.soldier.soldier_opts.soldier_buttons.<1>.icon@icon = "icons/<2>"
			*node:parent.soldier.soldier_opts.soldier_buttons.<1>.icon@background = "icons/circle"
		} else {
			*node:parent.soldier.soldier_opts.soldier_buttons.<1>.icon@icon = "icons/<2>0"
			*node:parent.soldier.soldier_opts.soldier_buttons.<1>.icon@background = "icons/circle0"
		}
	}

	// Select the soldier from the squad selection
	confunc hudselect
	{
		*cvar:i = 0;
		while ( *cvar:i < 12 ) {
			*node:root.soldier.squad.sld<cvar:i>@bordercolor = "0.527 0.6 0.21 1"
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i
		*node:root.soldier.squad.sld<1>@bordercolor = "0.824 0.917 0.437 1"
		cmd "show_oweapon;"
	}

	confunc huddisable {
		cmd "updateactorvalues <1> \"\" \"0\" \"1\" \"0\" \"1\" \"0\" \"1\" \"0\" \"\";"
		*node:root.soldier.squad.sld<1>@bgcolor = "0 0.1 0 0.7"
		*node:root.soldier.squad.sld<1>@onMouseEnter = { }
		*node:root.soldier.squad.sld<1>@onMouseLeave = { }
	}

	string overlay
	{
		string  "_Please wait"
		color   "1 0 0 1"
		font	"f_verybig_bold"
		ghost   true
		pos			 "0 0"
		size	"1024 768"
		contentalign	ALIGN_UC
	}

	// ==================
	// round events
	// ==================
	confunc startround
	{
		call *node:root.reactions@removeAllChild
		*node:root.overlay@invis = true
	}
	confunc endround
	{
		*node:root.overlay@invis = false
		cmd "hud_movemode;"
	}

	//This is called when a rescuezone is available in the given map
	confunc enable_rescuezone
	{
		*node:root.game_controls.show_rescue@invis = false
	}

	//This is called when no rescuezone is available in the given map
	confunc disable_rescuezone
	{
		*node:root.game_controls.show_rescue@invis = true
	}

	//This is called by the button to show or hide the rescue zone
	confunc toggle_rescuezone
	{
		if (*cvar:cl_map_draw_rescue_zone eq 0) {
			*cvar:cl_map_draw_rescue_zone = 2
			*node:root.game_controls.show_rescue.icon@tooltip = "_Hide rescue zone"
		} else {
			*cvar:cl_map_draw_rescue_zone = 0
			*node:root.game_controls.show_rescue.icon@tooltip = "_Show rescue zone"
		}
	}

	confunc updateLevelStatus {
		if (<1> == 1) {
			cmd "lvlchange 0;"
		}
	}

	confunc updateactorstatus {
		if ( <2> eq "bleeding" ) {
			if ( <3> != 0 ) {
				*node:root.soldier.squad.sld<1>@bgcolor = "0.7 0.1 0 0.7"
				*node:root.soldier.squad.sld<1>@onMouseEnter = { *node:this@bgcolor = "0.9 0 0 1" }
				*node:root.soldier.squad.sld<1>@onMouseLeave = { *node:this@bgcolor = "0.7 0.1 0 0.7" }
			} else {
				*node:root.soldier.squad.sld<1>@bgcolor = "0 0.1 0 0.7"
				*node:root.soldier.squad.sld<1>@onMouseEnter = { *node:this@bgcolor = "0 0 0 1" }
				*node:root.soldier.squad.sld<1>@onMouseLeave = { *node:this@bgcolor = "0 0.1 0 0.7" }
			}
		}
	}

	func onWindowOpened
	{
		cmd "music_change battlescape;"
		call *node:root.reactions@removeAllChild

		// Input soldier information squad selection
		*cvar:i = 0;
		while ( *cvar:i < 12 ) {
			*node:root.soldier.squad.sld<cvar:i>@num = <cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.head@src = *cvar:mn_head<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.health@max = *cvar:mn_hpmax<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.health@current = *cvar:mn_hp<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.tu@max = *cvar:mn_tumax<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.tu@current = *cvar:mn_tu<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.morale@max = *cvar:mn_moralemax<cvar:i>
			*node:root.soldier.squad.sld<cvar:i>.morale@current = *cvar:mn_morale<cvar:i>
			*cvar:sldcurhand<cvar:i> = "r"
			*cvar:i = ( *cvar:i + 1 )
		}
		delete *cvar:i

		// Select the current soldier and update the squad selection
		cmd "hudselect 0;"

		// Sets the attribute displayed in the squad panel bars
		*cvar:squad_bar = "health"

		// Hide the rescue zone on startup
		*cvar:cl_map_draw_rescue_zone = 0

		// Notices when the weapon has changed and calls the confunc that
		// shows or hides the otherweapon
		cmd "ui_addcvarlistener mn_lweapon show_oweapon;"
		cmd "ui_addcvarlistener mn_rweapon show_oweapon;"
	}
}

// Inventory and stats window
window inv_stats_hud
{
	{
		pos			"236 60"
		size		"560 418"
		bgcolor		"0 0.15 0.1 0.7"
		bordercolor	"0.58 0.81 0.758 0.7"
		border		"2"
		onMouseEnter	{
			*node:this@bgcolor = "0 0.21 0.158 0.7"
		}
		onMouseLeave	{
			*node:this@bgcolor = "0 0.15 0.1 0.7"
		}
	}
	// Top bar
	string name
	{
		string			*cvar:mn_name
		pos				"8 0"
		size			"300 25"
		font			"f_small_bold"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_LL
		ghost			true
	}
	string rank
	{
		string			*cvar:mn_chrrank
		pos				"8 25"
		size			"300 15"
		font			"f_verysmall"
		color			"0.56 0.81 0.76 1"
		longlines		LONGLINES_CHOP
		contentalign	ALIGN_UL
		ghost			true
	}
	controls drag
	{
		pos		"0 0"
		size	"560 40"
	}
	button close
	{
		icon			"icons/windowclose"
		tooltip			"_Close"
		pos				"530 10"
		size			"18	18"
		onClick			{ cmd "ui_pop;" }
	}
	button next
	{
		icon			"icons/windownext"
		tooltip			"_Next soldier"
		pos				"506 10"
		size			"18	18"
		onClick			{ cmd "actor_next;" }
	}
	button prev
	{
		icon			"icons/windowprev"
		tooltip			"_Previous soldier"
		pos				"482 10"
		size			"18	18"
		onClick			{ cmd "actor_prev;" }
	}
	// Inventory area
	image rank_img
	{
		pos 		"10 46"
		size 		"64 64"
		src 		*cvar:mn_chrrank_img
	}
	model actor
	{
		{
			src		*cvar:mn_body
			skin	*cvar:mn_body_skin
			anim	"stand_menu"
			origin	"0 -25 0"
			angles	"0 82 90"
			scale	"5 5 5"
			pos		"86 81"
			size	"191 262"
		}
		model head
		{
			src		*cvar:mn_head
			skin	*cvar:mn_head_skin
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	}
	image inv_bg
	{
		pos		"10 59"
		size	"351 349"
		src		"/ui/inv_bg"
	}
	container headgear	{ pos "88 85" }
	container backpack	{ pos "236 59" }
	container right		{ pos "13 146" }
	container left		{ pos "236 146" }
	// Armour is not switchable in combat - thus no display of "container armour" needed.
	container belt		{ pos "13 204" }
	container holster	{ pos "285 204" }
	container floor		{ pos "10 282" }

	button bt_load
	{
		pos "255 35"
		contentalign ALIGN_CL
		string  "_0/0 Kg"
		color "0.56 0.81 0.76 1"
		flashcolor "1 0 0 1"
		size "106 25"
		onClick { cmd "hud_updateactorload;" }
	}

	confunc inv_actorload {
		*node:root.bt_load@string = "<1>"
		*node:root.bt_load@tooltip = "<2>"
		if ( <3> > 0 ) { *node:root.bt_load@color = "0 1 0 1" }
		elif ( <3> < 0 ) { *node:root.bt_load@color = "1 1 0 1" }
		else { *node:root.bt_load@color = "0.56 0.81 0.76 1" }
		if ( <4> > 0 ) { *node:root.bt_load@flash = true }
		else { *node:root.bt_load@flash = false }
	}

	// Abilities panel
	// @TODO: Remove "mediocre" dsc text. Look at CL_ActorGetSkillString, cl_team.c
	panel abilities
	{
		{
			pos		"370 50"
			size		"180 120"
			ghost		true
		}
		StatBar strength
		{
			{
				pos		"0 0"
			}
			string label { string "_Strength" }
			bar valuebar { current *cvar:mn_vpwr }
			string valuenum { string *cvar:mn_tpwr }
		}
		StatBar speed
		{
			{
				pos		"0 30"
			}
			string label { string "_Speed" }
			bar valuebar { current *cvar:mn_vspd }
			string valuenum { string *cvar:mn_tspd }
		}
		StatBar accuracy
		{
			{
				pos		"0 60"
			}
			string label { string "_Accuracy" }
			bar valuebar { current *cvar:mn_vacc }
			string valuenum { string *cvar:mn_tacc }
		}
		StatBar mind
		{
			{
				pos		"0 90"
			}
			string label { string "_Mind" }
			bar valuebar { current *cvar:mn_vmnd }
			string valuenum { string *cvar:mn_tmnd }
		}
	}
	// Skills panel
	panel skills
	{
		{
			pos			"370 180"
			size		"180 120"
			ghost		true
		}
		StatBar cls
		{
			{
				pos		"0 0"
			}
			string label { string "_CQB" tooltip "_Close Quarters Battle" }
			bar valuebar { current *cvar:mn_vcls }
			string valuenum { string *cvar:mn_tcls }
		}
		StatBar ass
		{
			{
				pos		"0 30"
			}
			string label { string "_Assault" }
			bar valuebar { current *cvar:mn_vass }
			string valuenum { string *cvar:mn_tass }
		}
		StatBar snp
		{
			{
				pos		"0 60"
			}
			string label { string "_Sniper" }
			bar valuebar { current *cvar:mn_vsnp }
			string valuenum { string *cvar:mn_tsnp }
		}
		StatBar exp
		{
			{
				pos		"0 90"
			}
			string label { string "_Explosives" }
			bar valuebar { current *cvar:mn_vexp }
			string valuenum { string *cvar:mn_texp }
		}
	}

	// ======
	// wounds
	// ======
	panel wounds
	{
		pos		"370 310"
		size		"180 64"
		layout		LAYOUT_TOP_DOWN_FLOW
	}

	panel info
	{
		{
			pos		"370 384"
			size		"180 30"
		}
		smallstring mis_label
		{
			pos		"0 0"
			size		"160 15"
			longlines	LONGLINES_CHOP
			contentalign	ALIGN_UL
			string		"_Missions"
		}
		smallstring mis_kills
		{
			pos		"0 15"
			size		"160 15"
			longlines	LONGLINES_CHOP
			contentalign	ALIGN_UL
			string		"_Aliens killed"
		}
		smallstring missions
		{
			pos		"160 0"
			size		"20 15"
			longlines	LONGLINES_CHOP
			contentalign	ALIGN_UR
			string		*cvar:mn_chrmis
		}
		smallstring kills
		{
			pos		"160 15"
			size		"20 15"
			longlines	LONGLINES_CHOP
			contentalign	ALIGN_UR
			string		*cvar:mn_chrkillalien
		}
	}

	// <1> Body part type (id)
	// <2> Severity of bleeding, if 0 all wounds are treated
	// (Note: Robots don't bleed, this just means they have serious damage in this part)
	// <3> Tooltip
	confunc actor_wounds {
		call *node:root.wounds@createChild("wound_<1>", "cmpWound")
		if (<2> > 0) {
			*node:root.wounds.wound_<1>.wound_icon@icon = "icons/<1>_wound"
		} else {
			*node:root.wounds.wound_<1>.wound_icon@icon = "icons/<1>_wound_treated"
		}
		*node:root.wounds.wound_<1>.wound_string@string = "<3>"
		*node:root.wounds.wound_<1>@tooltip = "<3>"
	}

	confunc update_actor_state {
		call *node:root.wounds@removeAllchild
		cmd "hud_updateactorwounds;"
		cmd "hud_updateactorload;"
	}

	confunc show_hide_inv_stats
	{
		if ( *cvar:inv_stats_window_open == 1 ) {
			cmd "ui_close inv_stats_hud;"
		} else {
			cmd "ui_push inv_stats_hud;"
		}
	}

	func onWindowOpened {
		*cvar:inv_stats_window_open = 1
		cmd "ui_addcvarlistener cl_selected update_actor_state;"
		cmd "update_actor_state;"
		if ( *cvar:mn_base_count < 1 ) {
			*node:root.info@invis = true
		}
	}

	func onWindowClosed {
		cmd "ui_removecvarlistener cl_selected update_actor_state;"
		delete *cvar:inv_stats_window_open
	}
}
