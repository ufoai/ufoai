// ==============
// SKIRMISH MENU
// ==============

// @TODO
// - Team selection broken
// - Make dropship/ufo selection the renders with a prev/next select option
// - The map selection system needs to be fit into new style a bit better.
//   border, etc.
// - Seem to be missing *cvar:cl_equip and *cvar:cl_equipname (player equipment)
// - use cmpTeamPanel

component cmpMapPreviewButton extends radiobutton
{
	{
		icon		"icons/map_prev"
		size		"132 140"
		cvar		"*cvar:mn_svmapid"
		stringValue	" "
		padding		10
		onClick {
			cmd "mn_selectmap \"<node:this.id@string>\";"
		}
	}

	image preview {
		pos		"2 2"
		size	"128 96"
		src		""
		ghost	true
	}

	image frameOverlay {
		pos		"2 2"
		size	"128 96"
		src		"maps/shots/map_ico_overlay"
		ghost	true
	}

	string name {
		string			""
		contentalign	ALIGN_CC
		font			"f_verysmall"
		pos				"8 101"
		size			"110 32"
		ghost			true
	}

	string id {
		invis	true
		string	""
		pos		"14 96"
		size	"100 32"
	}

	// @param <1> name of the map
	// @param <2> image name of the map
	// @todo we should not need more than a map id
	func init {
		*node:parent.name@string = "_<1>"
		*node:parent.id@string = "<1>"
		*node:parent@stringValue = "<1>"
		*node:parent.preview@src = "maps/shots/<2>"
	}
}

window skirmish extends cgame_download
{
	{
		background	"ui/main_bg"
	}

	// Toolbar across the top of the screen
	MainMenuToolbar mmtoolbar { }

	// ===================
	// MAP SELECTION PANEL
	// ===================
	panel sk_map_sel
	{
		{
			size		"504 435"
			pos			"0 90"
			bgcolor		"0.4 0.52 0.5 0.2"
		}

		panel maplist {
			pos				"0 0"
			size			"504 435"
			layout			LAYOUT_COLUMN
			layoutColumns	3
			layoutMargin	5
			padding			5

			onViewChange {
				*node:root.sk_map_sel.maplist_scroll@fullsize = <fullsize>
				*node:root.sk_map_sel.maplist_scroll@current = <viewpos>
				*node:root.sk_map_sel.maplist_scroll@viewsize = <viewsize>
			}

			// subnode buttons added at runtime
		}

		/**
		 * Callback to receive maplist from client
		 * @param <1> "begin", "add" or "end"
		 * @param <2> If <1> == "add", map id, else nothing
		 * @param <3> If <1> == "add", map picture name, else nothing
		 */
		confunc	sk_maplist_listener {
			if (param1 eq "begin") {
				*cvar:count = 0
				*cvar:mn_svmapid = ""
				call *node:root.sk_map_sel.maplist@removeAllChild
			} elif (param1 eq "add") {
				call *node:root.sk_map_sel.maplist@createchild("map<cvar:count>", "cmpMapPreviewButton")
				call *node:root.sk_map_sel.maplist.map<cvar:count>.init(param2, param3)
				*cvar:count = (*cvar:count + 1)
				if (*cvar:mn_svmapid eq "") {
					*cvar:mn_svmapid = param2
				}
			} elif (param1 eq "end") {
				delete *cvar:count
				cmd "mn_selectmap \"<cvar:mn_svmapid>\""
			} else {
				cmd "echo sk_maplist_listener: command \"<1>\" unknown."
			}
		}

		vscrollbar maplist_scroll {
			image		"ui/scrollbar_v"
			pos			"484 0"
			height		"435"
			onChange	{ *node:root.sk_map_sel.maplist@viewpos = <current> }
		}
	}

	// =================
	// OPTIONS PANEL
	// =================
	panel sk_options
	{
		{
			size		"510 435"
			pos			"517 90"
			bgcolor		"0.4 0.52 0.5 0.2"
		}

		button btn_map
		{
			size 		"125 30"
			pos 		"0 0"
			string		"_MAP"
			font		"f_small_bold"
			bgcolor		"0.4 0.515 0.5 1"
			color		"0 0 0 0.9"
			onClick		{ call *node:root.toggle_sk_map }
		}

		button btn_squad
		{
			size 		"125 30"
			pos 		"135 0"
			string		"_SQUAD"
			font		"f_small_bold"
			bgcolor		"0.4 0.515 0.5 0.25"
			color		"1 1 1 0.5"
			onClick		{ call *node:root.toggle_sk_squad }
		}

		button btn_download
		{
			size 		"125 30"
			pos 		"270 0"
			string		"_DOWNLOAD"
			font		"f_small_bold"
			bgcolor		"0.4 0.515 0.5 1"
			color		"0 0 0 0.9"
			onClick		{ call *node:root.toggle_sk_download }
		}

		MainMenu3Btn btn_begin
		{
			size 			"250 30"
			pos 			"260 405"
			string			"_BEGIN"
			bgcolor			"0.38 0.48 0.36 1"
			onMouseEnter 	{ *node:this@bgcolor = "0.59 0.78 0.56 1" }
			onMouseLeave 	{ *node:this@bgcolor = "0.38 0.48 0.36 1" }
			onClick			{ cmd "sk_start;" }
		}

		MainMenuExitBtn btn_back
		{
			size 			"250 30"
			pos 			"0 405"
			string			"_BACK"
			onClick			{ cmd "ui_pop;" }
		}

		// ============
		// MAP SETTINGS
		// ============
		panel sk_map_opts
		{
			{
				pos			"10 50"
				size		"490 360"
			}

			MapOptions panel_map
			{
				{
					pos		"0 0"
					size	"490 360"
				}
				SetEnemyCount sk_sel_enemy
				{
					{
						pos		"0 200"
					}
				}

				SetEnemyRush sk_sel_rush
				{
					{
						pos		"0 230"
					}
				}

				SelDropship map_sel_drop
				{
					{
						pos		"0 260"
					}
				}
				SelUFO map_sel_ufo
				{
					{
						pos		"0 300"
					}
				}
			}
		}

		// ==============
		// SQUAD SETTINGS
		// ==============
		panel sk_squad_opts
		{
			{
				pos			"0 50"
				size		"510 320"
			}

			EnemyEquip enemy_equip
			{
				{
					pos		"10 0"
				}
			}

			PlayerSquad player_squad
			{
				{
					pos		"0 40"
				}
			}
		}
		// List of available downloads
		panel sk_download_opts
		{
			{
				pos				"0 50"
				size			"510 320"
				layout			LAYOUT_TOP_DOWN_FLOW
				layoutMargin	5
				invis			true
			}

			panel downloadable
			{
				{
					pos		"35 70"
					size	"440 270"
					layout			LAYOUT_TOP_DOWN_FLOW
					layoutMargin	2
				}
			}
		}
	}

	// Switch display when user chooses Map/Squad options
	func toggle_sk_map {
		*node:root.sk_options.btn_map@bgcolor = "0.4 0.515 0.5 1"
		*node:root.sk_options.btn_map@color = "0 0 0 0.9"
		*node:root.sk_options.btn_squad@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_squad@color = "1 1 1 0.5"
		*node:root.sk_options.btn_download@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_download@color = "1 1 1 0.5"
		*node:root.sk_options.sk_map_opts@invis = false
		*node:root.sk_options.sk_squad_opts@invis = true
		*node:root.sk_options.sk_download_opts@invis = true
	}
	func toggle_sk_squad {
		*node:root.sk_options.btn_squad@bgcolor = "0.4 0.515 0.5 1"
		*node:root.sk_options.btn_squad@color = "0 0 0 0.9"
		*node:root.sk_options.btn_map@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_map@color = "1 1 1 0.5"
		*node:root.sk_options.btn_download@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_download@color = "1 1 1 0.5"
		*node:root.sk_options.sk_map_opts@invis = true
		*node:root.sk_options.sk_squad_opts@invis = false
		*node:root.sk_options.sk_download_opts@invis = true
	}
	func toggle_sk_download {
		*node:root.sk_options.btn_download@bgcolor = "0.4 0.515 0.5 1"
		*node:root.sk_options.btn_download@color = "1 1 1 0.9"
		*node:root.sk_options.btn_squad@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_squad@color = "0 0 0 0.5"
		*node:root.sk_options.btn_map@bgcolor = "0.4 0.515 0.5 0.25"
		*node:root.sk_options.btn_map@color = "1 1 1 0.5"
		*node:root.sk_options.sk_map_opts@invis = true
		*node:root.sk_options.sk_squad_opts@invis = true
		*node:root.sk_options.sk_download_opts@invis = false
		cmd	"web_listcgame 0;"
	}
	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_dropships
	{
		*node:root.sk_options.sk_map_opts.panel_map.map_sel_drop@invis = <1>
	}
	/**
	 * @param<1> boolean to hide or show
	 */
	confunc skirmish_hide_ufos
	{
		*node:root.sk_options.sk_map_opts.panel_map.map_sel_ufo@invis = <1>
	}

	/**
	 * @brief Add a server stored file to the list of downloadable files
	 * @param <1> The file id (string)
	 * @param <2> The category
	 * @param <3> The user id of the owner
	 * @param <4> The file name
	 * @param <5> The title
	 * @param <6> Own file = 1, other users file = 0
	 */
	confunc cgamefiles_add
	{
		call *node:root.root.sk_options.sk_download_opts.downloadable@createChild("download<1>-<2>-<3>", "downloadentry")
		*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.category@integer = <2>
		*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.userid@integer = <3>
		*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.filename@string = <4>
		*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.download@string = <5>
		if ( <6> == 1 ) {
			*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.delete@invis = false
		} else {
			*node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>.delete@invis = true
		}
	}

	confunc cgamefiles_nofilesfound
	{
		call *node:root.root.sk_options.sk_download_opts.downloadable@createChild("nofiles", "string")
		*node:root.root.sk_options.sk_download_opts.downloadable.nofiles@string = "_No files uploaded yet"
	}

	/**
	 * @param <1> The file id (string)
	 * @param <2> The category
	 * @param <3> The user id of the owner
	 */
	confunc cgame_deletesuccessful
	{
		call *node:root.root.sk_options.sk_download_opts.downloadable.download<1>-<2>-<3>@delete
	}

	confunc cgamefiles_clear
	{
		call *node:root.root.sk_options.sk_download_opts.downloadable@removeAllChild
	}

	// =====
	// INIT
	// =====
	func onWindowOpened
	{
		// TODO: Allow changing the difficulty via menu
		cmd	"game_setmode;set ai_equipment multiplayer_alien;set difficulty 0;"
		cmd "set mn_serverday 1;"
		cmd "mn_getmaps;define cl_ugvs 0;define cl_teamdef phalanx;"
		*cvar:mn_svmapid = "none"
		cmd "mn_requestmaplist sk_maplist_listener;"
		cmd	"game_teamcomments;sk_initequip cl_equip;sk_initequip ai_equipment;"
		cmd "define g_endlessaliens 0;"

		// Resets map choices when Skirmish menu reloaded
		call *node:root.toggle_sk_map

		// Resets squad choices when Skirmish menu reloaded
		cmd "toggle_custom_team_opts;"
	}

	func onWindowActivate
	{
		cmd "game_teamcomments;"
	}

	// Fuzzy screen effect (see assets.ufo)
	fuzzyScreen overlay { }
}
